context RatingContext {
    
    //Entity
    structure RatingProvider<entity> {
        long id<identifier>, //GeneratedValue
        long providerId<neverEmpty>,
        long userId<neverEmpty>,
        double score<neverEmpty>, //Must be between 1 and 5
        string comment,
        date timestamp //initialized with current time on creation
    }

    //Entity
    structure RatingVehicle<entity> {
        long id<identifier>, //GeneratedValue
        long vehicleId<neverEmpty>,
        long userId<neverEmpty>,
        double score<neverEmpty>, //Must be between 1 and 5
        string comment,
        date timestamp //initialized with current time
    }

    collection RatingProviderList {RatingProvider ratingProvider}

    collection RatingVehicleList {RatingVehicle ratingVehicle}

    structure RatingProviderRepository<repository> {
        //Query("SELECT AVG(r.score) FROM RatingProvider r WHERE r.providerId = :providerId")
        function double findAverageScoreByProviderId(long providerId), //Optional<Double>
        procedure deleteByUserId(long userId),
        function RatingProviderList findByProviderId(long providerId),
        function RatingProviderList findByUserId(long customerId),
        function RatingProvider findByUserIdAndProviderId(long userId, long providerId) //Optional<RatingProvider>
    }

    structure RatingVehicleRepository<repository> {
        //Query("SELECT AVG(r.score) FROM RatingVehicle r WHERE r.vehicleId = :vehicleId")
        function double findAverageScoreByVehicleId(long vehicleId), //Optional<Double> 
        procedure deleteByUserId(long userId),
        procedure deleteByVehicleId(long vehicleId),
        function RatingVehicleList findByVehicleId(long providerId),
        function RatingVehicleList findByUserId(long customerId),
        function RatingVehicle findByUserIdAndVehicleId(long userId, long vehicleId) //Optional<RatingVehicle>
    }

    structure RatingService<applicationService> {
        function RatingVehicle rateVehicle(RatingVehicle rating),
        function RatingProvider rateProvider(RatingProvider rating),
        function double getAverageRatingForVehicle(long vehicleId),
        function double getAverageRatingForProvider(long providerId),
        procedure deleteAllCustomerRatings(long userId), //Transactional
        procedure deleteAllProviderRatings(long providerId), //Transactional
        procedure deleteAllVehicleRatings(long vehicleId) //Transactional
    }

}
